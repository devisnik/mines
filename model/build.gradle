apply plugin: 'java'

def archivesBaseName = "de.devisnik.mine.model"
def versionName = '1.3.4'
def version = new Version(baseVersion: versionName)

sourceCompatibility = 1.6

dependencies {
	testCompile 'junit:junit:4.+'
}

jar {
	manifest {
		attributes 'Implementation-Title': archivesBaseName, 'Implementation-Version': version
	}
}

task sources_jar(type: Jar, dependsOn:classes) {
	description = 'Assembles a jar archive containing the sources.'
	group = 'build'
	classifier = 'sources'
	from sourceSets.main.allSource
}

task link_jars(dependsOn:[jar, sources_jar]) {
	description = 'Creates a properties file conforming Android project standards to link source-jar to code-jar'
	ext.jarFile = new File("${jar.archivePath}")
	ext.jarProperties = new File("${jar.archivePath}.properties")
	ext.sourceJarFile = new File("${sources_jar.archivePath}")
	inputs.file files(jarFile, sourceJarFile)
	outputs.file jarProperties
	doLast {
		file(jarProperties).write "src=${sources_jar.archiveName}\n"
	}
}

artifacts {
	archives sources_jar
	archives(link_jars.jarProperties) {
		type = 'jar.properties' // ensure correct file extension
	}  
}

assemble.dependsOn.add('link_jars')

class Version {
	String baseVersion
	String date = new Date().format("yyyyMMddHHmm")

	String toString() {
		"$baseVersion.$date"
	}
}
